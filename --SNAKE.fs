." Snake is loading and will" CR
." auto-start..." CR

( SNAKE )

( GAME VARIABLES )

( SNAKE HEAD COORDINATES )
VARIABLE SNAKEX
VARIABLE SNAKEY

( SNAKE DIRECTION )
VARIABLE DIRECTION
VARIABLE INPUTDIR ( USED DURING INPUT )

( FOOD COORDINATES )
VARIABLE FOODX
VARIABLE FOODY

( SCORE )
VARIABLE SCORE
VARIABLE HIGHSCORE

( DELAY )
VARIABLE DELAY

( INITIALISE ALL VARIABLES EXCEPT HIGHSCORE )

: AVINIT
 CQ ( ERASE THE QUEUE )
 
 32 SNAKEX !
 24 SNAKEY !
 
 RIGHT DIRECTION !
 
 16 FOODX !
 12 FOODY !
 
 0 SCORE !
 150 DELAY !
;

( INITIALISE THE DISPLAY )

: DINIT
 CLS
 REFR
 
 ( DRAW HORIZONTAL LINES )
 64
 BEGIN
  DUP
  0<> ( NOT ZERO YET ? )
 WHILE
  1- ( ADVANCE ALONG DISPLAY )
  DUP 0 PLOT ( TOP LINE )
  DUP 47 PLOT ( BOTTOM LINE )
 REPEAT
 DROP
 REFR
 
 ( DRAW VERTICAL LINES )
 48
 BEGIN
  DUP
  0<> ( NOT ZERO YET ? )
 WHILE
  1- ( ADVANCE ALONG DISPLAY )
  0 OVER PLOT ( LEFT LINE )
  63 OVER PLOT ( RIGHT LINE )
 REPEAT
 DROP
 REFR
;

0 HIGHSCORE ! ( RESET HIGH SCORE ONLY ONCE )

( RETURN THE DIRECTION OPPOSITE )
: OPPOSITE
 CASE
  UP OF DOWN ENDOF
  RIGHT OF LEFT ENDOF
  LEFT OF RIGHT ENDOF
  DOWN OF UP ENDOF
 ENDCASE
;

( DURING THE DELAY, KEEP GETTING INPUT. IF IT'S ZERO, IGNORE IT. IF IT'S A BUTTON, RECORD IT. )

: GETDIR
 DELAY @ MSBEGIN
  BUTTON ( BUTTON )
  ?DUP 0 = UNLESS
   DIRECTION @ OPPOSITE ( BUTTON OPPOSITE )
   INVERT AND ( ELIMINATE OPPOSITE )
   15 AND ( IGNORE NON-ARROWS )
   DIRECTION @ INVERT AND ( ZERO OUT CURR )
   INPUTDIR ! ( WRITE OUT DIRECTION )
  THEN
 MSUNTIL
 INPUTDIR @
 ( ONLY SET DIRECTION IF CHANGED )
 0 <> IF
  INPUTDIR @
  DIRECTION ! ( SET DIRECTION )
 THEN
;

: GETFOOD
 FOODX @ FOODY @
;

: PLOTFOOD
 GETFOOD PLOT
;

: UNPLOTFOOD
 GETFOOD UNPLOT
;

: GETHEAD
 SNAKEX @ SNAKEY @
;

: MOVE
 PLOTFOOD
 FDQ FDQ UNPLOT ( ERASE TAIL )
 DIRECTION @ CASE
  LEFT OF -1 SNAKEX +! ENDOF
  RIGHT OF 1 SNAKEX +! ENDOF
  UP OF -1 SNAKEY +! ENDOF
  DOWN OF 1 SNAKEY +! ENDOF
 ENDCASE
 GETHEAD SWAP FEQ FEQ ( EQ NEW POSITION )
 GETHEAD TEST ( RETURNED FROM HERE )
 GETHEAD PLOT
 REFR
 UNPLOTFOOD
 GETHEAD PLOT ( REPLOT IF EATING FOOD )
;

( RESETS FOOD TO RANDOM PLACE )

: NEWFOOD
 BEGIN
  RND RND PXWRAP ( CHOOSE NEW SPOT )
  2DUP TEST ( TAKEN? )
  0 =
 UNTIL
 
 ( PLACE FOOD )
 FOODY !
 FOODX !
 
 ( INCREASE SCORE )
 1 SCORE +!
 
 ( DOUBLE EQ HEAD TO MAKE SNAKE LONGER )
 GETHEAD SWAP FEQ FEQ ( EQ NEW POSITION )
 
 ( REDUCE DELAY )
 DELAY @ 1 > IF -1 DELAY +! THEN
;

: GAMEOVER
 SCORE @ HIGHSCORE @ > IF
  SCORE @ HIGHSCORE !
 THEN
 
 CLS
 ." This game's score was "
 SCORE @ . CR
 ." The current high score is "
 HIGHSCORE @ . CR
 ." Type GAME to replay."
 RESET
 QUIT
;

( COMPRESS X AND Y INTO SINGLE NUMBER )
: COMPRESS 64 * + ;

: EAT
 ( IF 1, SNAKE HIT SOMETHING )
 ( IT MUST BE FOOD, OR IT'S GAME OVER )
 1 = IF
  GETHEAD COMPRESS
  GETFOOD COMPRESS
  = IF
   NEWFOOD
  ELSE
   GAMEOVER
  THEN
 THEN
;

( RUN SNAKE )

: GAME
 DINIT
 AVINIT
 GETHEAD SWAP FEQ FEQ ( EQ INITIAL POSITION )
 REFR
 BEGIN
  GETDIR
  MOVE
  EAT
 AGAIN
;

GAME


